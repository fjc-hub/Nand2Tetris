// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {

    static Array powerOf2;
    static Array ScreenAddr;
    static boolean Color;

    /** Initializes the Screen. */
    function void init() {
        var int i;

        let ScreenAddr = 16384;
        let Color = true;  //  overwrite old value of this static variable's address

        let powerOf2 = Array.new(16);
        let i = 1;
        let powerOf2[0] = 1;
        while (i < 16) {
            let powerOf2[i] = powerOf2[i-1] + powerOf2[i-1];
            let i = i + 1;
        }

        return ;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        var int i, j;
        let i = 0;
        let j = 0;
        while (i < 256) {
            while (j < 512) {
                let ScreenAddr[i * 512 + j] = 0;
                let j = j + 1;
            }
            let i = i + 1;
        }
        return ;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let Color = b;
        return ;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int addr;
        var int idx;

        let addr = 32 * y + (x / 16);
        let idx = x - (x / 16 * 16);

        if (Color) {
            let ScreenAddr[addr] = ScreenAddr[addr] | powerOf2[idx];
        } else {
            let ScreenAddr[addr] = ScreenAddr[addr] & ~(powerOf2[idx]);
        }
        return ;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        // error case
        if ((x1 < 0) | (x1 > 511) | (x2 < 0)| (x2 > 511) | (y1 < 0) | (y1 > 255) | (y2 < 0) | (y2 > 255)) {
            do Sys.error(8);
        }
        // vertical
        if (x1 = x2) {
            do Screen.drawVertical(x1, y1, x2, y2);
            return ;
        }
        // horizontal
        if (y1 = y2) {
            do Screen.drawHorizontal(x1, y1, x2, y2);
            return ;
        }
        // other
        if (y1 < y2) {  // 注意原点在左上角
            if (x1 < x2) {
                do Screen.drawFromTopLeft(x1, y1, x2, y2);
            } else {
                do Screen.drawFromTopRight(x1, y1, x2, y2);
            }
            return ;
        } else {
            if (x1 > x2) {
                do Screen.drawFromTopLeft(x2, y2, x1, y1);
            } else {
                do Screen.drawFromTopRight(x2, y2, x1, y1);
            }
        }
        return ;
    }

    function void drawVertical(int x1, int y1, int x2, int y2) {
        var int tmp;
        if (y1 > y2) {
            let tmp = y2;
            let y2 = y1;
            let y1 = tmp;
        }
        while (~(y1 > y2)) {
            do Screen.drawPixel(x1, y1);
            let y1 = y1 + 1;
        }
        return ;
    }

    function void drawHorizontal(int x1, int y1, int x2, int y2) {
        var int tmp;
        if (x1 > x2) {
            let tmp = x2;
            let x2 = x1;
            let x1 = tmp;
        }

        while (~(x1 > x2)) {
            do Screen.drawPixel(x1, y1);
            let x1 = x1 + 1;
        }
        return ;
    }

    // point(x1, y1) is top-left of point(x2,y2) 
    // VECTOR: (a, b), (dx, dy) 
    function void drawFromTopLeft(int x1, int y1, int x2, int y2) {
        var int a, b, dx, dy;
        var int diff;
        let a = 0;
        let b = 0;
        let dx = x2 - x1;
        let dy = y2 - y1;
        let diff = 0;   // diff = a * dy - b * dx

        while (~(a > dx) & ~(b > dy)) {     // (a <= dx) & (b <= dy)  
            do Screen.drawPixel(x1 + a, y1 + b);
            if (diff < 0) {
                let a = a + 1;
                let diff = diff + dy;
            } else {
                let b = b + 1;
                let diff = diff - dx;
            }
        }
        return ;
    }

    // point(x1, y1) is top-right of point(x2,y2) 
    // VECTOR: (-a, b), (-dx, dy)
    function void drawFromTopRight(int x1, int y1, int x2, int y2) {
        var int a, b, dx, dy;
        var int diff;
        let a = 0;
        let b = 0;
        let dx = x1 - x2;
        let dy = y2 - y1;
        let diff = 0;       // diff = a * dy - b * dx

        while (~(a > dx) & ~(b > dy)) {     // (a <= dx) & (b <= dy)  
            do Screen.drawPixel(x1 - a, y1 + b);
            if (diff < 0) {
                let a = a + 1;
                let diff = diff + dy;
            } else {
                let b = b + 1;
                let diff = diff - dx;
            }
        }
        return ;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        var int idx, end, dy;

        let dy = Math.abs(y1 - y2);
        if (y1 < y2) {
            let dy = y2 - y1;
            let idx = y1;
            let end = y2;
        } else {
            let dy = y1 - y2;
            let idx = y2;
            let end = y1;
        }

        while (~(idx > end)) {  //  idx <= end
            do Screen.drawLine(x1, idx, x2, idx);
            let idx = idx + 1;
        }
        return ;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dx, dy;

        let dy = r;
        while (~(dy < -r)) { // dy >= -r
            let dx = Math.sqrt((r * r) - (dy * dy));
            do Screen.drawLine(x - dx, y + dy, x + dx, y + dy);
            let dy = dy - 1;
        }

        return ;
    }
}
